repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/ktor' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
    }
}

apply plugin: 'kotlin'

sourceSets {
    main.kotlin.srcDirs = [ 'src' ]
    test.kotlin.srcDirs = [ 'test' ]
    main.resources.srcDirs = [ 'resources', 'content']
}

repositories {
    jcenter()
    maven { url "https://kotlin.bintray.com/ktor" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile 'com.atlassian.commonmark:commonmark:0.11.0'
    compile 'com.atlassian.commonmark:commonmark-ext-heading-anchor:0.11.0'
}

processResources {
    doLast {
        println "copy dist to resources/public"
        copy {
            from "../dist"
            into "build/resources/main/public"
        }
    }
}

if (distJar.toBoolean()) {

    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: 'application'
    mainClassName = "io.ktor.server.netty.DevelopmentEngine"


    shadowJar {
        baseName = 'play.data2viz.io'
        classifier = null
        version = null
    }
}
